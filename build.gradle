buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'net.minecrell.licenser' version '0.3'
}

group 'com.dumptruckman.kaddy'
version '1.0-SNAPSHOT'

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'net.minecrell.licenser'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        testCompile group: 'junit', name: 'junit', version:'4.12'
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version:'1.6.1'
        testCompile group: 'org.powermock', name: 'powermock-api-easymock', version:'1.6.1'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version:'1.6.1'
        testCompile group: 'org.easymock', name: 'easymock', version:'3.4'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }

    task copyDependencies(type: Copy) {
        from configurations.compile
        into "${project.rootDir}/libs"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    license {
        header rootProject.file("license/HEADER.txt")
        include '**/*.kt'
        include '**/*.java'
        newLine false
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
        from "{project.rootDir}/LICENSE.txt"
    }

    artifacts {
        archives sourcesJar
    }
}

subprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.bastian-oppermann.de'
        }
        maven {
            url 'http://repo.onarandombox.com/content/groups/public/'
        }
    }

    dependencies {
        compile 'io.github.microutils:kotlin-logging:1.4.8'
        compile 'org.slf4j:slf4j-api:1.7.21'
        compile 'ch.qos.logback:logback-classic:1.1.3'
        compile 'de.btobastian.javacord:javacord:2.0.15:shaded'
        compile 'com.google.auto.service:auto-service:1.0-rc3'
        compile 'io.github.config4k:config4k:0.3.2'
        compile 'com.github.plugkit:plugkit:1.0-SNAPSHOT'
        compile 'com.dumptruckman.dtmlibs:logback-logging:1.0-SNAPSHOT'
    }
}

project(':sample-plugin') {
    task copySample(type: Copy) {
        dependsOn build
        from jar
        into "${project.rootDir}/plugins"
    }
}